plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.asciidoctor.convert" version "1.5.9.2"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation("org.junit.vintage:junit-vintage-engine") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    /* Rest Docs */
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    runtimeOnly 'com.h2database:h2'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
    include "**/*Test.class"
}

ext {
    // 스니펫 생성 위치 변수에 저장
    set('snippetsDir', file("build/generated-snippets"))
}

// static/docs 폴더 비우기
asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor {
    inputs.dir snippetsDir // ext 에서 설정해준 snippetsDir로
    dependsOn test // 문서 생성 전 테스트가 실행되도록 test 에 종속 설정

    // source가 없으면 .adoc파일을 전부 html로 만들어버림
    // source 지정시 특정 adoc만 HTML로 만든다.

}

task copyAsciidoctor(type: Copy) {
    dependsOn asciidoctor
    from "build/docs/asciidoc"
    into "src/main/resources/static/docs"
}

// build 의 의존작업 명시
build {
    dependsOn copyAsciidoctor
}

//bootRun
bootRun {
    dependsOn copyAsciidoctor
}

bootJar {
    // 빌드 전 문서 생성 확인
    dependsOn asciidoctor
    // 생성된 문서를 static/docs 에 복사
    from ("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}